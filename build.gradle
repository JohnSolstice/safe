buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.4'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    }
}

ext {
    ci = "true".equals(System.getenv("CI"))
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

    // Manifest version information!
    versionMajor = 1
    versionMinor = 4
    versionPatch = 5
    versionBuild = 0 // bump for dogfood builds, public betas, etc.

    versionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName = "${versionMajor}.${versionMinor}.${versionPatch}"

    compileSdkVersion = 24
    buildToolsVersion = "24.0.2"
    targetSdkVersion = 24
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://jitpack.io'
        }

    }

    apply plugin: 'findbugs'
    findbugs {
        ignoreFailures = true
    }

    apply plugin: 'pmd'
    pmd {
        ignoreFailures = true
    }
}
